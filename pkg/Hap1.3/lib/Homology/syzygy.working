#(C) Graham Ellis 2005-2006

#####################################################################
#####################################################################
InstallGlobalFunction(Syzygy,
function(R,g)
local
	Homotopy, Syz,  i, Elts, Mult, n,
	R203, R1023, R1203, R102;

n:=Length(g);

#####################################################################
Elts:=function(x);
return Position(R.elts,x);
end;
#####################################################################

#####################################################################
Mult:=function(x,w);	#x is in the group G.
return List(w,y->[y[1],Position(R.elts,x*R.elts[y[2]])]);
end;
#####################################################################

#####################################################################
Homotopy:=function(i,w)
local ans, x;
ans:=[];

for x in w do
ans:=AddWords(ans,R.homotopy(i,x));
od;
return ans;
end;
#####################################################################

if n<2 or n>3 then 
Print("ERROR: Syzygy() is so far only implemented for 2- and 3-syzygies. \n");
return fail;
fi;

##################### IF N=2 ########################################
if n=2 then
Syz:= R.homotopy(0,[1,Elts(g[1])]);
Syz:=AddWords(Syz,Mult(g[1],R.homotopy(0,[1,Elts(g[2])])));
Syz:=AddWords(Syz,NegateWord(R.homotopy(0,[1,Elts(g[1]*g[2])]))) ;
return Syz;
fi;
##################### FI N=2 ########################################

##################### IF N=3 ########################################
R203:=Mult(g[1], Homotopy(1,Syzygy(R,[g[2],g[3]])));
R1023:=Homotopy(1,Syzygy(R,[g[1],g[2]*g[3]]));
R1203:=Homotopy(1,NegateWord(Syzygy(R,[g[1]*g[2],g[3]])));
R102:=Homotopy(1,NegateWord(Syzygy(R,[g[1],g[2]])));

Syz:=AddWords(R203,R1023);
Syz:=AddWords(Syz,R1203);
Syz:=AddWords(Syz,R102);

return Syz;
#################### FI N=3 #########################################


end);
#####################################################################
#####################################################################


#####################################################################
#####################################################################
InstallGlobalFunction(StandardCocycle,
function(arg)
local 
	 R,f,n,q,Standard,Homotopy;
R:=arg[1];
f:=arg[2];
n:=arg[3];
if Length(arg)>3 then q:=arg[4]; else q:=0; fi;

#####################################################################
Homotopy:=function(w)
local ans, x;
ans:=[];

for x in w do
ans:=AddWords(ans,R.homotopy(n-1,x));
od;
return ans;
end;
#####################################################################

#####################################################################
Standard:=function(g,h,k)
local S,v,x;

S:=Homotopy(Syzygy(R,[g,h,k]));
Apply(S,x->x[1]);

v:=List([1..R.dimension(n)],x->0);

for x in S do
v[AbsoluteValue(x)]:=v[AbsoluteValue(x)] + SignInt(x);
od;

if q=0 then
return v*f;
else
return v*f mod q; fi;
end;
#####################################################################

return Standard;
end);
#####################################################################
#####################################################################
